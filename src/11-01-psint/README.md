# Programaci√≥n (Fundamentos)
Aqu√≠ conoceremos los fundamentos de programaci√≥n utilizando un pseudoc√≥digo llamado PSeInt. Si tu no sabes nada de programaci√≥n deber√≠as comenzar por aqu√≠.

<div class="text-center">
    <img :src="$withBase('/img/programar.gif')" alt="icono visual studio code git">
</div>

## Ventajas
- Estos conceptos te servir√°n para la mayor√≠a de los lenguajes de programaci√≥n.
- Pasar estas definiciones a Javascript ser√° muy f√°cil.
- Nos ayuda visualmente como pensar como programadores.
- **Esta gu√≠a tiene ejemplos orientados a Javascript.**

## PSeInt
PSeInt es una herramienta para asistir a un estudiante en sus primeros pasos en programaci√≥n. Mediante un simple e intuitivo **pseudolenguaje** en espa√±ol (complementado con un editor de diagramas de flujo), le permite centrar su atenci√≥n en los conceptos fundamentales de la algoritmia computacional, minimizando las dificultades propias de un lenguaje y proporcionando un entorno de trabajo con numerosas ayudas y recursos did√°cticos.

## Instalaci√≥n
- [Descargar](http://pseint.sourceforge.net/)

Una vez instalado seleccionar la opci√≥n 3:
<div class="text-center">
    <img :src="$withBase('/img/pseint-1.JPG')" alt="icono visual studio code git">
</div>

## Herramientas
Tenemos las t√≠picas herramientas como nuevo, abrir, etc. Las importantes ser√°n:

- Corregir Indentado: Las buenas pr√°cticas indican que un c√≥digo bien indentado ser√° de f√°cil lectura.
- Ejecutar
- Ejecutar paso a paso 
- Dibujar diagrama de flujo

<div class="text-center">
    <img :src="$withBase('/img/indentar.gif')" alt="icono visual studio code git">
</div>

## Click derecho
Sobre el texto "Algoritmo" damos click derecho nos saldr√° una opci√≥n de definici√≥n, lo cual es sumamente √∫til para revisar conceptos:

- Algoritmo: Una secuencia de instrucciones es una lista de una o m√°s instrucciones y/o estructuras de control. Comienza con la palabra clave Algoritmo (o alternativamente Proceso, son sin√≥nimos) seguida del nombre del programa, luego le sigue una secuencia de instrucciones y finaliza con la palabra FinAlgoritmo (o FinProceso).

Ejemplo SUMA:
```js
// este es el ejemplo m√°s simple de esta ayuda, 
// toma dos numeros, los suma y muestra el resultado

Algoritmo Suma


    // para cargar un dato, se le muestra un mensaje al usuario
    // con la instrucci√≥n Escribir, y luego se lee el dato en 
    // una variable (A para el primero, B para el segundo) con 
    // la instrucci√≥n Leer

    Escribir "Ingrese el primer numero:"
    Leer A

    Escribir "Ingrese el segundo numero:"
    Leer B


    // ahora se calcula la suma y se guarda el resultado en la
    // variable C mediante la asignaci√≥n (<-)
    
    C <- A+B


    // finalmente, se muestra el resultado, precedido de un 
    // mensaje para avisar al usuario, todo en una sola
    // instrucci√≥n Escribir
    
    Escribir "El resultado es: ",C

FinAlgoritmo
```

## Paso a paso
Ejecutar opci√≥n paso a paso ü¶∂ü¶∂ y seleccionar explicar en detalle c/paso:
<div class="text-center">
    <img :src="$withBase('/img/pseint-2.JPG')" alt="icono visual studio code git">
</div>

## Variables
Primero tenemos que conocer el concepto de variable:

- En programaci√≥n una variable es un espacio de memoria el cual nos servir√° para almacenar un tipo de dato con un valor correspondiente.
- Imagina como una caja que guarda un tipo de dato/valor.

<div class="text-center">
    <img :src="$withBase('/img/pseint-3.JPG')" alt="icono visual studio code git">
</div>

## Datos (pseint)
Los tipos posibles son NUMERO/REAL/ENTERO, LOGICO, CARACTER/TEXTO/CADENA. 

- **NUMERO, NUMERICO y REAL** son sin√≥nimos para el tipo de datos num√©rico b√°sico, que puede almacenar tanto n√∫meros reales como enteros.
- **LOGICO** s√≥lo puede tomar los valores VERDADERO y FALSO, pero cuando se lee una variable ya definida como l√≥gica, el usuario puede ingresar tambi√©n las abreviaciones V y F, o 0 y 1.
- **CARACTER, TEXTO y CADENA** son sin√≥nimos para definir variables de tipo car√°cter. Estas pueden contener cero, uno o m√°s caracteres arbitrarios y no tienen una longitud m√°xima.


```js
Algoritmo super_suma
	textoUno = "Esto es un String";
	Escribir textoUno;
	
	numeroUno = 25;
	Escribir numeroUno;
	
	estadoActual = Verdadero;
	Escribir estadoActual;
FinAlgoritmo
```

:::tip Asignaci√≥n
- El signo `=` en Javascript se conoce como asignaci√≥n (permite almacenar un valor a una variable).
- Se eval√∫a la expresi√≥n de la derecha y luego se le asigna el resultado a la variable de la izquierda.
:::

## Reglas (en Javascript)
Al momento de declarar una variable recuerda:
- Javascript es un lenguaje sensible a may√∫sculas y min√∫sculas. <br> No es lo mismo **"NombreUsuario"** a **"nombreUsuario"**.
- No use guiones bajos al comienzo de los nombres de las variables ‚Äî esto se usa en ciertas construcciones de JavaScript para significar cosas espec√≠ficas, por lo que puede resultar confuso.
- No uses n√∫meros al comienzo de las variables. Esto no est√° permitido y provoca un error.
- Una convenci√≥n segura a seguir es la llamada "min√∫scula may√∫sculas intercaladas" (Camel case) üê´.

## Punto y coma
El punto y coma indica el final de una sentencia, pero Javascript lo detecta autom√°ticamente, por ende puedes o no colocarlo. A excepci√≥n del siguiente ejemplo:
<div class="text-center">
    <img :src="$withBase('/img/pseint-4.JPG')" alt="icono visual studio code git">
</div>

## Leer
- La instrucci√≥n Leer permite ingresar informaci√≥n desde el ambiente. 
- Si una variable no existe, se crea durante la lectura. Si la variable existe se pierde su valor anterior ya que tomar√° el valor nuevo, raz√≥n por la cual se dice que la lectura es "destructiva" (destruye el valor que ten√≠a previamente la variable).

```js
Imprimir "Ingrese su nombre"
leer nombreUsuario
Imprimir "Bienvenido: " , nombreUsuario
```

## Concatenaci√≥n
Nos sirve para unir una o m√°s variables, tambi√©n lo puedes mezclar con diferentes tipos de datos.

## Pr√°ctica
```js
Algoritmo super_suma
	
	Imprimir "Ingrese su nombre"
	leer nombreUsuario
	Imprimir "Bienvenido: " , nombreUsuario
	
	Imprimir "Ingrese primero n√∫mero"
	leer primerNumero
	
	Imprimir "Ingrese segundo n√∫mero"
	leer segundoNumero
	
	resultadoSuma = primerNumero + segundoNumero
	
	Imprimir nombreUsuario , " el resultado es: " , resultadoSuma
	
FinAlgoritmo
```

:::tip
Las variables traten de hacerlas lo m√°s descriptivas posibles, este es un programa simple pero a futuro tendr√°s cientos de variables declaradas. Adem√°s si alguien revisa el c√≥digo tambi√©n se da una idea de lo que est√° ocurriendo.
:::

## Operadores
Existen diferentes tipos de operadores

- Operadores Aritm√©ticos o Algebraicos o Matem√°ticos.
- Operadores de Comparaci√≥n / Relacionales.
- Operadores l√≥gicos.

## Operadores Aritm√©ticos
En programaci√≥n y matem√°ticas, los operadores aritm√©ticos son aquellos que manipulan los datos de tipo num√©rico, es decir, permiten la realizaci√≥n de operaciones matem√°ticas (sumas, restas, multiplicaciones, etc.).. [m√°s info](https://developer.mozilla.org/es/docs/Learn/JavaScript/First_steps/Math#operadores_aritm%C3%A9ticos)

Adici√≥n: Suma dos n√∫meros juntos.
```js
resultado = 20 + 10;
Mostrar resultado;
```

Resta: Resta el numero de la derecha del de la izquierda.
```js
resultado = 20 - 10;
Mostrar resultado;
```

Multiplicaci√≥n: Multiplica dos n√∫meros juntos.
```js
resultado = 20 * 10;
Mostrar resultado;
```

Divisi√≥n: Divide el n√∫mero de la izquierda por el de la derecha.
```js
resultado = 20 / 10;
Mostrar resultado;
```

Sobrante (tambi√©n llamado m√≥dulo): Retorna el restante despu√©s de dividir el n√∫mero de la izquierda en porciones enteras del de la derecha.
```js
resultado = 10 % 3;
Mostrar resultado;
```

## Operadores relacionales
Los operadores relacionales o comparaci√≥n definidos por PseInt son id√©nticos a los que definen las matem√°ticas: mayor que (>), menor que (<), mayor o igual (>=), menor o igual (<=), igual que (==) y distinto de (!=, <>).

```js
resultado = 10 > 11;
Mostrar resultado;
```

```js
resultado = 10 <= 10;
Mostrar resultado;
```

```js
resultado = 10 == 11;
Mostrar resultado;
```

```js
resultado = 10 <> 11;
Mostrar resultado;
```

## Operadores l√≥gicos
Los operadores l√≥gicos se usan para combinar dos valores Booleanos y devolver un resultado verdadero, falso o nulo. Los operadores l√≥gicos tambi√©n se denominan operadores Booleanos.

- && (y): Si los dos son verdaderos devuelve verdadero.
- || (o): Basta con que uno sea verdadero para que devuelva verdadero.
- ! (no): Negaci√≥n

```js
resultado = 20 > 10 y 50 < 100
Mostrar resultado
```

```js
resultado = !Verdadero
Mostrar resultado
```

## Estructuras de control
En lenguajes de programaci√≥n, las estructuras de control **permiten modificar el flujo de ejecuci√≥n de las instrucciones de un programa**.

Condicionales
- if/else (Si ocurre algo, haz esto, sino, haz lo esto otro...)
- ?: operador ternario (Operador ternario: Equivalente a If/else , m√©todo abreviado.)
- switch (Estructura para casos espec√≠ficos: Similar a varios If/else anidados.)

Repetitivas o iterativas
- while
- do... while
- for

## if/else (si/no)
Un if en programaci√≥n se utiliza para evaluar una expresi√≥n condicional: **si se cumple la condici√≥n (es verdadera), ejecutar√° un bloque de c√≥digo.**

<div class="text-center">
    <img :src="$withBase('/img/pseint-5.JPG')" alt="icono visual studio code git">
</div>

Intenta hacer esto:
<div class="text-center">
    <img :src="$withBase('/img/pseint-6.JPG')" alt="icono visual studio code git">
</div>

```js
Algoritmo super_suma
	Mostrar "Ingrese un n√∫mero del 1 al 10"
	Leer numeroIngresado
	Si ( numeroIngresado <= 10 ) Entonces
		Escribir 'Correcto!!'
	SiNo
		Escribir 'Super mal!!'
	FinSi
FinAlgoritmo
```

```js
Algoritmo super_suma
	Mostrar "adivina n√∫mero del 1 al 10"
	leer nUser
	nMaquina = 6
	Si (nUser == nMaquina)
		Mostrar "Adivinaste!!"
	SiNo
		Si(nMaquina > nUser)
			Mostrar "Era m√°s alto"
		SiNo
			Mostrar "Era m√°s bajo"
		FinSi
		Mostrar "El n√∫mero era: " , nMaquina
	FinSi
FinAlgoritmo
```


## switch (segun)
Esta instrucci√≥n permite ejecutar opcionalmente varias acciones posibles, dependiendo del valor almacenado en una variable. Al ejecutarse, se eval√∫a el contenido de la variable y se ejecuta la secuencia de instrucciones asociada con dicho valor. 

<div class="text-center">
    <img :src="$withBase('/img/pseint-7.JPG')" alt="icono visual studio code git">
</div>

```js
Algoritmo menu
	Mostrar "¬øAburrido? elija una opci√≥n:"
	Mostrar "1. Lectura"
	Mostrar "2. Cine"
	Mostrar "3. Juego"
	Mostrar "Ingrese n√∫mero"
	
	Leer opcionElegida
	
	Segun opcionElegida
		1: 
			Escribir "Lectura recomendada"
			Escribir "* Principito"
		2:
			Escribir "Pel√≠cula recomendada"
			Escribir "* Matrix"
		3: 
			Escribir "Juego recomendado"
			Escribir "* NFS"
		De Otro Modo:
			Escribir "opci√≥n no v√°lida"
	FinSegun
	
FinAlgoritmo
```

## while (mientras)
Crea un bucle o loop que ejecuta una sentencia especificada mientras cierta condici√≥n se eval√∫e como verdadera.

<div class="text-center">
    <img :src="$withBase('/img/pseint-8.JPG')" alt="icono visual studio code git">
</div>

```js
Algoritmo while
	n <- 0
	Mientras (n<=5) Hacer
		Escribir 'vuelta: ' , n
		n <- n+1
	FinMientras
FinAlgoritmo
```

## Juego adivinar
```js
Algoritmo super_suma
	numAzar = Aleatorio(1,10)
	Mostrar numAzar
FinAlgoritmo
```

```js
Algoritmo aprender_while
	// genero un n√∫mero aleatorio
	nAzar = Aleatorio(1,10)

	intentos = 3
	
	Mostrar "adivina n√∫mero del 1 al 10, tienes: " , intentos , " intentos"
	Mostrar "Ingresa n√∫mero"
	leer nUser
	
    // si el n√∫mero no es igual entrar√° al while
	Mientras (nAzar <> nUser y intentos > 1) 
		
        Si (nAzar > nUser)
			Mostrar "muy bajo"
		SiNo
			Mostrar "muy alto"
		FinSi

        // restamos 1 intento
		intentos = intentos - 1
		Mostrar "Te quedan... " , intentos , " intentos!"

        // caputamos n√∫mero user
		Leer nUser
	FinMientras
	
	Si (nAzar == nUser)
		Mostrar "Adivinaste!!"
	SiNo
		Mostrar "Perdiste!! se te acabaron los intentos!"
	FinSi

FinAlgoritmo
```

## Array (Arreglos)
Los arrays son objetos **similares a una lista** cuyo prototipo proporciona m√©todos **para efectuar operaciones de recorrido** y de mutaci√≥n. Tanto la longitud como el tipo de los elementos de un array son variables.

```js
Algoritmo array_for
	
	Dimension frutas[3]
	frutas[1] = "Manzana"
	frutas[2] = "Platano"
	frutas[3] = "Sand√≠a"
	
	Mostrar frutas[1]
	Mostrar frutas[2]
	Mostrar frutas[3]
	
FinAlgoritmo
```

En Javascript es din√°mico por ende no necesitamos indicar la dimensi√≥n:
```js
let frutas = ["Manzana", "Platano", "Sand√≠a"]
```

## for (para)
La instrucci√≥n **Para (for)** ejecuta una secuencia de instrucciones un n√∫mero determinado de veces. 
<div class="text-center">
    <img :src="$withBase('/img/pseint-10.JPG')" alt="icono visual studio code git">
</div>

```js
Algoritmo array_for
	
	Dimension frutas[3]
	frutas[1] = "Manzana"
	frutas[2] = "Platano"
	frutas[3] = "Sand√≠a"
	
	Para i Desde 1 Hasta 3 Hacer
		Mostrar frutas(i)
	FinPara

FinAlgoritmo
```


## for of
<div class="text-center">
    <img :src="$withBase('/img/pseint-9.JPG')" alt="icono visual studio code git">
</div>

```js
Algoritmo for_of
	
	Dimension frutas[3]
	frutas[1] = "Manzana"
	frutas[2] = "Platano"
	frutas[3] = "Sand√≠a"
	
	Para Cada elemento de frutas Hacer
		Mostrar elemento
	FinPara
	
FinAlgoritmo
```

## Funciones
Las funciones son uno de los bloques de construcci√≥n fundamentales en JavaScript. Una funci√≥n en JavaScript es similar a un procedimiento ‚Äî un **conjunto de instrucciones que realiza una tarea o calcula un valor**, pero para que un procedimiento califique como funci√≥n, debe tomar alguna entrada y devolver una salida donde hay alguna relaci√≥n obvia entre la entrada y la salida. Para usar una funci√≥n, debes definirla en alg√∫n lugar del √°mbito desde el que deseas llamarla.

Funci√≥n si argumento o par√°metros
```js
Funcion Saludar
	Mostrar "Hola Bienvenido!"
FinFuncion

Algoritmo ejemplo_func
	
	Saludar()

FinAlgoritmo
```

Con argumentos
```js
Funcion Saludar (nombreUsuario)
	Mostrar "Hola Bienvenido! " , nombreUsuario
FinFuncion

Algoritmo ejemplo_func
	
	Saludar("Juanito")

FinAlgoritmo
```

Con retorno
```js
Funcion resultado = Sumar ( n1, n2 )
	resultado = n1 + n2
Fin Funcion

Algoritmo ejemplo_func
	
	Mostrar "Ingrese n√∫mero 1"
	leer numUno
	Mostrar "Ingrese n√∫mero 2"
	leer numDos
	
	Mostrar "Suma es: " , Sumar(numUno, numDos)

FinAlgoritmo
```

## Qu√© sigue...
<div class="text-center">
    <img :src="$withBase('/img/javascript.gif')" alt="icono visual studio code git">
</div>

<p class="text-center">
<b>En la pr√≥xima secci√≥n aterrizaremos todos estos conceptos a Javascript!</b>
</p>
